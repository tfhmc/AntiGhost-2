plugins {
	id 'fabric-loom' version '1.11-SNAPSHOT'
	id "com.modrinth.minotaur" version "2.+"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

repositories {
	maven {
		url = "https://maven.fabricmc.net"
	}
	maven {
		url = "https://maven.terraformersmc.com/releases/com/terraformersmc/modmenu/"
	}
}

ext.Versions = new Properties()
Versions.load(file("Versionfiles/mcversion-1.21.5.properties").newReader())

ext.projectVersion = "1.3.0"

version = "${Versions['minecraft_version']}-fabric${Versions['fabric_versiononly']}-${project.projectVersion}"

loom {
    mixin.defaultRefmapName = "AntiGhost-refmap.json"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

dependencies {
    minecraft  "com.mojang:minecraft:${Versions['minecraft_version']}"
    mappings   "net.fabricmc:yarn:${Versions['yarn_mappings']}:v2"
    modImplementation "net.fabricmc:fabric-loader:${Versions['loader_version']}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${Versions['fabric_version']}"
}

java {
    sourceCompatibility = 21
    targetCompatibility = 21
    withSourcesJar()
}

jar {
    archiveBaseName.set("AntiGhost")
	from "LICENSE"
}